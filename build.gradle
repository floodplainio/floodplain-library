plugins {
    id 'eclipse'
    id 'io.quarkus' version "${quarkusPluginVersion}"
    id 'org.jetbrains.kotlin.jvm' version "1.3.41"
    id "org.jetbrains.kotlin.plugin.allopen" version "1.3.41"
    id "com.google.protobuf" version "0.8.12"
}
apply from: 'gradle/dependencies.gradle'

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        gradlePluginPortal()
        google()
    }
}

subprojects {
    version = rootProject.ext.floodplain_version
    apply plugin: "java"
    apply plugin: "eclipse"
    apply plugin: "maven-publish"
    apply plugin: 'io.quarkus'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply from: "$rootProject.projectDir/gradle/common.gradle"
    group = "io.floodplain"

    if (name == 'replication-protobuf') {
        apply plugin: 'com.google.protobuf'
    }
    if (name == 'floodplain-quarkus') {
        apply plugin: 'org.jetbrains.kotlin.plugin.allopen'
        apply plugin: 'io.quarkus'
    }

    publishing {
        publications {
            "$project.name"(MavenPublication) {
                groupId = "io.floodplain"
                artifactId = project.name
                version = rootProject.ext.floodplain_version
                from components.java
            }
        }
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/floodplainio/floodplain-library")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_PACKAGES_PAN")
                }
            }
        }
    }
}

