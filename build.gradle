plugins {
    id "eclipse"
    id "org.jetbrains.kotlin.jvm" version "1.3.72"
    id "org.jetbrains.kotlin.plugin.allopen" version "1.3.72"
    id "com.github.johnrengelman.shadow" version '5.2.0'
    id "com.google.protobuf" version "0.8.12"
    id "org.jlleitschuh.gradle.ktlint" version "9.2.1"
    id "com.github.hierynomus.license-base" version "0.15.0"
    id "com.palantir.graal"  version "0.7.0-5-g838c2ab"
    id "org.jetbrains.dokka" version "0.10.1"
}


apply from: "gradle/dependencies.gradle"
apply from: "gradle/shared.gradle"
apply plugin: "maven-publish"

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        gradlePluginPortal()
        google()
        maven {
            url "http://packages.confluent.io/maven"
        }
    }

    license {
        header rootProject.file("gradle/LICENSE_HEADER.txt")
        mapping("java", "SLASHSTAR_STYLE")
        mapping("kt", "SLASHSTAR_STYLE")
        ext.year = "2020"
        ext.name = "Frank Lyaruu"
    }
}

// BOM publication doesn't work well yet.
publishing {
    publications {
        maven(MavenPublication) {
            customizePom(pom)
            groupId = "io.floodplain"
            artifactId = "library"
            version = rootProject.ext.floodplain_version
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    dependencyManagement {
                        dependencies {
                            project.configurations.each { conf ->
                                conf.dependencies.each { dep ->
                                    dependency {
                                        groupId "${dep.group}"
                                        artifactId "${dep.name}"
                                        version "${dep.version}"
                                    }
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

subprojects {
//    systemProperty "release", findProperty("release")
    version = rootProject.ext.floodplain_version
    apply plugin: "java"
    apply plugin: "eclipse"
    apply plugin: "maven-publish"
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply from: "$rootProject.projectDir/gradle/common.gradle"
    apply plugin: "org.jetbrains.dokka"

    apply plugin: "signing"
    group = "io.floodplain"

    if (name == "replication-protobuf") {
        apply plugin: "com.google.protobuf"
    }
    apply plugin: "com.github.hierynomus.license-base"
    publishing {
        publications {
            "$project.name"(MavenPublication) {
                customizePom(pom)
                groupId = "io.floodplain"
                artifactId = project.name
                version = rootProject.ext.floodplain_version
                from components.java
                artifact sourceJar
                artifact javadocJar
            }
        }
        repositories {
            maven {
                name = "Snapshots"
                url = uri("https://oss.sonatype.org/content/repositories/snapshots/")

                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("CENTRAL_USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("CENTRAL_PASSWORD")
                }
            }
            maven {
                name = "Staging"
                url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")

                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("CENTRAL_USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("CENTRAL_PASSWORD")
                }
            }
        }
    }
//    signing {
//        sign publishing.publications."$project.name"(MavenPublication)
//    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description "Transforms CDC streams in Kotlin"
            name "Floodplain"
            url "https://floodplain.io"
            organization {
                name "Floodplain"
                url "https://floodplain.io"
            }
            issueManagement {
                system "GitHub"
                url "https://github.com/floodplainio/floodplain-library/issues"
            }
            licenses {
                license {
                    name "Apache License 2.0"
                    url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    distribution "repo"
                }
            }

            scm {
                url "https://github.com/floodplainio/floodplainio/floodplain-library"
                connection "scm:git:git://github.com/floodplainio/floodplain-library.git"
                developerConnection "scm:git:ssh://git@github.com:floodplainio/floodplain-library.git"
            }
            developers {
                developer {
                    name "Frank Lyaruu"
                }
            }
        }
    }
}


